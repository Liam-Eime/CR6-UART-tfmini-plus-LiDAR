'CR6 Datalogger, UART for TFMini Plus LiDAR
'Date: 06/12/2024
'Program author: Liam Eime

'The following program demonstrates receiving data from a continuous output serial sensor; the TFmini Plus LiDAR.
'The TFmini Plus uses the UART protocol for the exchange of serial data.

'The program performs the following:
  'Establishes the ports C1 and C2 for serial communication.
  'Reads the serial data from the LiDAR sensor, obtaining distance, signal strength and internal chip temperature.

' Declare Constants
Const BaudRate = 115200

' Declare Public Variables
Public Distance     :  Units Distance = cm     'Distance measurement
Public Strength                                'Signal strengh of LiDAR reading
Public Temperature  :  Units Temperature = Â°C  'Internal chip temperature
Public NBytesReturned                          'Number of bytes returned by SerialInRecord

' Declare Private Variables
Dim DistanceRaw(2) As Long
Dim StrengthRaw(2) As Long
Dim TemperatureRaw(2) As Long
Dim tmp(6) As Long

' Define DataTables
DataTable (MeasurementsTable, True, -1)
  DataInterval (0, 1, Sec, 10)
  Sample (1,Distance,FP2)
  Sample (1,Strength,FP2)
  Sample (1,Temperature,FP2)
EndTable

BeginProg
  
  'Open the serial port, serial open format must be 19 for the TFmini Plus
  SerialOpen (ComC1,BaudRate,19,0,1000)

  Scan (10,mSec, 0, 0)
    
    'The output data is of the form: 0x59 0x59 DistLow DistHigh StrengthLow StrengthHigh TempLow TempHigh Checksum
      'Read only the 6 bytes of data after the &H5959 header
    SerialInRecord (ComC1,tmp(),&H5959,6,0,NBytesReturned,00)
    
    'Move the bytes from the temporary storage to the respective raw data variables
    MoveBytes (DistanceRaw(1),3,tmp(1),0,1)
    MoveBytes (DistanceRaw(2),3,tmp(1),1,1)
    MoveBytes (StrengthRaw(1),3,tmp(1),2,1)
    MoveBytes (StrengthRaw(2),3,tmp(1),3,1)
    MoveBytes (TemperatureRaw(1),3,tmp(2),0,1)
    MoveBytes (TemperatureRaw(2),3,tmp(2),1,1)
    
    'Convert the raw data to meaningful values
    Distance = HexToDec (Hex (DistanceRaw(2)) & Hex (DistanceRaw(1)))
    Strength = HexToDec (Hex (StrengthRaw(2)) & Hex (StrengthRaw(1)))
    Temperature = HexToDec (Hex (TemperatureRaw(2)) & Hex (TemperatureRaw(1))) / 8 - 256  'Temp = Temp / 8 - 256 from datasheet
    
    'Store the measurements in the data table
    CallTable MeasurementsTable
    
  NextScan
  
EndProg